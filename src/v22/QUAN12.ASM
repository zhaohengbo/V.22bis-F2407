;
; DATE          :       7-14-86
;
; SUBROUTINE    :       QUAN12
; PURPOSE       :       1200 BPS QUANTIZER
;
; TASK: GIVEN COMPLEX RECEIVED POINT Y = YREAL + j YIMAG
;       MAP Y TO ONE OF THE POINTS A, B, C, OR D WHERE;
;           A = 1.0 + j 3.0
;           B = -1.0 + j 3.0
;           C = -1.0 - j 3.0
;           D = 1.0  - j 3.0
;
; ENTRY CONDITION:
;   THE REAL AND IMAGINARY PART OF THE COMPLEX
;   Y ARE IN LOCATIONS YREAL & YIMAG RESPECTIVELY.
;
; EXIT CONDITION:
;   THE QUANTIZED VALUES OF THE REAL AND IMAGINARY
;   PARTS ARE RETURNED IN PREAL AND PIMAG
;
; DESCRIPTION:
;   THE COMPLEX POINT Y IS ROTATED THROUGH ANGLE THETA
;   WHERE THETA = ARCTAN (1/2), VIA COMPLEX MULTIPLYING
;   IT BY EXP (j THETA).  THE REAL AND IMAGINARY PARTS OF
;   THE ROTATED COMPLEX POINT ARE STORED IN RAM LOCATIONS
;   RTEMP AND ITEMP RESPECTIVELY.  SUBROUTINE QDETER IS
;   THEN USED TO DETERMINE WHICH QUADRANT (0,1,2 OR 3)
;   THE COMPLEX ROTATED POINT BELONGS TO. THE QUADRANT NO.
;   IS RETURNED IN RAM LOCATION CQUAD (FOR Current
;   QUADrant) AND DETERMINES WHICH POINT FROM THE SET
;   {A,B,C,D} IS TO BE OUTPUT AS THE QUANTIZED VALUE OF Y.
;   THESE POINTS ARE HELD IN A CONSTELLATION LOOK-UP
;   TABLE WITH START ADDRESS XCONST. THE ACTUAL LOCATION
;   OF THE REAL AND IMAGINARY PARTS OF A, B, C AND D ARE
;   AS FOLLOWS :
;
;     A IS IN LOCATIONS XCONST+2 AND XCONST+2+1
;     B IS IN LOCATIONS XCONST+10 AND XCONST+10+1
;     C IS IN LOCATIONS XCONST+18 AND XCONST+18+1
;     D IS IN LOCATIONS XCONST+26 AND XCONST+26+1
;
;     THIS POSITIONING OF THE CONSTELLATION POINTS ALLOWS
;     A SIMPLE WAY OF FORMING THE LOOK-UP ADDRESS TO EACH
;     OF THE ABOVE POINTS.  FIRST THE START ADDRESS OF THE
;     CONSTELLATION TABLE (i.e XCONST) IS LOADED INTO THE
;     ACCUMULATOR. THE CONTENT OF RAM LOCATION CURRQUAD IS
;     ADDED TO THE ACCUMULATOR WITH A LEFT SHIFT OF 2. THE
;     NUMERAL 1 IS NEXT ADDED TO THE ACCUMULATOR.
;     THIS GIVES THE ADDRESS OF THE CONSTELLATION POINT,
;     WITH THE REAL PART IN THE 8 LSBS AND THE IMAGINARY PART
;     IN THE 8 MSBS.
;
; NOTES:  1) YREAL, YIMAG, PREAL & PIMAG ARE REPRESENTED IN
;            Q12 FORMAT.
;
;         2) THETA = ARCTAN (1/2) = 26.565 DEGREES
;            COS (THETA) = 0.894427 = 3664 IN 13 BITS T/C
;            SIN (THETA) = 0.447213 = 1832 IN 13 BITS T/C
;
;         3) RTEMP = YREAL * COS(THETA) - YIMAG * SIN(THETA)
;            ITEMP = YIMAG * COS(THETA) + YREAL * SIN(THETA)
;
;
; REVISION INFO : MODIFIED 8-11-86 TO REPLACE RTEMP
;                 WITH PREAL AND ITEMP WITH PIMAG.
;                 MODIFIED 8-11-86 TO REPLACE CQUAD WITH
;                 TMP0.
;
;
;
;       FIRST COMPUTE RTEMP AS IN NOTE (3) ABOVE
;
;
QUAN12
	 LT    YREAL   ; LOAD T REGISTER WITH YREAL
     MPYK    3664    ; FORM PARTIAL RESULT
     PAC             ; TRANSFER PARTIAL RESULT INTO ACC
     LT      YIMAG   ; LOAD T REGISTER WITH YIMAG
     MPYK    1832    ; FORM THE SECOND PARTIAL RESULT
     SPAC            ; SUBTRACT FROM ACC TO FORM RTEMP
     SACH    PREAL,4 ; LEFT SHIFT OF 4 IS NECESSARY FOR DECIMAL
;			  POINT ADJUSTMENT.
;
;
;       NOW COMPUTE ITEMP AS IN NOTE (3) ABOVE
;
     MPYK    3664    ; REMEMBER T STILL HOLDS YIMAG
     PAC             ; TRANSFER PARTIAL RESULT TO ACC
     LT      YREAL   ; LOAD T WITH YREAL
     MPYK    1832    ; FORM THE 2ND PARTIAL RESULT
     APAC            ; FORM THE IMAGINARY PART OF ROTATED POINT
     SACH    PIMAG,4 ; LEFT SHIFT OF 4 IS NECESSARY FOR DECIMAL
;			  POINT ADJUSTMENT
;
;
;       NOW USE SUBROUTINE QDETER TO DETERMINE WHICH QUADRANT THE ROTATED
;       COMPLEX POINT BELONGS TO.
;
     CALL    QDTER   ; QUADRANT NO. IS RETURNED IN THE ACC; 1 stack
     SACL    TMP0    ; PUT AWAY FOR FUTURE USE
;
;
;       IMPLEMENT THE ADDRESSING MECHANISM DESCRIBED ABOVE
;
     LALK    XCONST  ; XCONST CONTAINS THE START ADDRESS
;			   OF THE CONSTELLATION TABLE
     ADD     TMP0,2  ; ADD THE QUADRANT OFFSET ADDRESS
     ADD     ONE     ; AND AN ADDITIONAL 1 FOR ADJUSTMENT
     TBLR    TMP1    ; LOOK UP THE REAL VALUE
     LAC     TMP1,8  ; ISOLATE THE QUAD PORTION IN LOWER ACC
     SACL    PREAL   ; STORE PREAL IN Q12
     SACH    PIMAG   ; STORE THE IN-PHASE IN PREAL
     LAC     PIMAG,8 ; CONVERT INTO Q12
     SACL    PIMAG   ;
     RET             ;
;
;       END OF SUBROUTINE QUAN12
;
